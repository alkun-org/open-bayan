IMPORT ct "https://alkun.org/bayan/common-type.bayan"
IMPORT place "https://alkun.org/bayan/place.bayan"
IMPORT api_ "https://alkun.org/bayan/api.bayan"

entity = {
    id = "people"
    name = "People"
    version = 23.06
    createdAt = "2023-06-01"
    status = "draft"

    data EXTEND ct.Data & place.Address & place.Geo & {
        fullName STR <2:64>
        birthday DATE
        gender ENUM ("male" "female")
        phone STR
        email = ct.Email
        website URL                 # personal website
        about STR <:500>
        tag EXTEND ct.Tag [:20]
        view NUM
    }

    index = {
        fullName STR <3>    # @fulltext
        about STR <3>       # @fulltext
        city STR            # @fulltext
        state STR           # @fulltext

        country = ct.Country
        gender ENUM ("male" "female")
        tag EXTEND ct.Tag [:20]
        geo = place.Geo        # @geo

        createdAt DATE
        hide BOOL
        control = ct.Data.control
    }

    api = {
        set = {
            permission = ("owner")
            request = {
                apiKey STR
                data EXTEND entity.data & {
                    UNSET eri & createdAt & updatedAt & control
                }
                REQUIRED apiKey & data
            }
        }

        delete = {
            permission = ("owner")
            request = {
                apiKey STR
                REQUIRED apiKey
            }
        }
    }
}