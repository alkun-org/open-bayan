IMPORT ct "https://alkun.org/bayan/common-type.bayan"
IMPORT api_ "https://alkun.org/bayan/api.bayan"

entity = {
    id = "say"
    name = "Say"
    description = "A simple communication space"
    version = 23.06
    createdAt = "2023-06-01"
    status = "draft"

    data EXTEND ct.Data & {
        title STR <10:150>
        content STR <10:20000>
        featImage = ct.Image        # 800px max wide, featured image for this post
        poll = ct.Poll
        tag EXTEND ct.Tag [1:5]
        language = ct.Language

        reaction = ct.Reaction
        comment = ct.Comment
        url URL                     # URL for this say
        thumbnail = ct.Image
        snippet STR                 # ~250 chars
        estimatedRead NUM           # In seconds, calc: (wordCount / 250 WordsPerMin) * 60sec
        view NUM
    }

    index = {
        title STR <3>       # @fulltext
        content STR <3>     # @fulltext
        user STR            # user eg: "ahmad"
        tag EXTEND ct.Tag [1:5]
        language = ct.Language
        reactionScore NUM

        createdAt DATE
        updatedAt DATE
        hide BOOL
        control = ct.Data.control
    }

    api = {
        delete = api_.Delete

        create = {
            permission = ("owner")
            request = {
                apiKey STR
                title = entity.data.title
                content = entity.data.content
                featImage = entity.data.featImage
                media EXTEND ct.Image [:8]      # attach media (only images for now) in your post
                poll = ct.Poll
                tag = entity.data.tag
                language = ct.Language
                hide BOOL

                REQUIRED apiKey & title & tag & content | featImage | poll
            }
            response = {
                eri = ct.Eri
                url URL
            }
        }

        update = {
            permission = ("owner")
            request EXTEND entity.api.create.request & {
                eri = ct.Eri
                UNSET title     # title cannot be update, create new post instead
                NULLABLE content & featImage & poll & language
                REQUIRED eri & apiKey & tag & content | featImage | poll
            }
            response = {
                url URL
            }
        }
    }
}