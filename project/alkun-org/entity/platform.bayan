IMPORT ct "https://alkun.org/bayan/common-type.bayan"
IMPORT api_ "https://alkun.org/bayan/api.bayan"

entity = {
    id = "platform"
    name = "Platform support"
    version = 23.06
    createdAt = "2023-06-01"
    status = "draft"

    api = {
        getBayan = {
            request = {
                name STR
                REQUIRED name
            }
            response = {
                dsl STR
                json STR
            }
        }

        compileBayan = {
            request = {
                dsl = {
                    name STR            # filename to use for import
                    source STR          # DSL source
                } []
                main STR                # which dsl to use as entry
                REQUIRED dsl & main
            }
            response = {
                json STR
            }
        }

        get = {
            # Direct access to entity-data object \
            private fields will be omitted unless apiKey provided and caller is data owner

            request = {
                eri = ct.Eri
                apiKey STR
                REQUIRED eri
            }
            response OBJ
        }

        query = {   # Generic entity search endpoint
            request EXTEND api_.OffsetPage & {
                apiKey STR
                select STR : ("eri")            # field to retrieve in ERI field format
                entity STR                      # Entity to search, eg: "people"
                search = STR <3>                # full-text search, <field> : "text", optional
                searchIn STR[]
                filter STR                          # filter string (refer Bayan Query DSL)
                order STR[:2] /^\w+(\(\-?\d*\.?\d*\,\-?\d*\.?\d*\))?:(asc|desc)$/    # ["<field><(lat,lon)>:<asc/desc>" ...], eg: ["createdAt:asc", "geo(-1.23,3.45):asc"]
                REQUIRED entity & select
            }
            response EXTEND api_.ResultSet & {
                item = {
                    highlight = {               # only from search field
                        field STR
                        ARB /^[a-zA-Z0-9]+$/ field
                    }
                    LOOSE true
                }[]
            }
        }

        hide = {
            permission = ("owner")
            request = {  # Enable/disable item
                eri = ct.Eri
                apiKey STR
                state BOOL
                REQUIRED eri & apiKey & state
            }
        }

        control = {
            permission = ("admin" "mod")
            request = {
                eri = ct.Eri
                apiKey STR
                state = ct.Data.control
                REQUIRED eri & apiKey & state
            }
        }
    }
}
